---
name: Dhall
filename: learndhall.dhall
contributors:
    - ["Gabriel Gonzalez", "http://www.haskellforall.com/"]
translators:
    - ["Taeyoon Kim", "https://github.com/partrita"]
---
Dhallì€ YAMLì— ëŒ€í•œ ë°˜ë³µì ì´ì§€ ì•Šì€ ëŒ€ì•ˆì„ ì œê³µí•˜ëŠ” í”„ë¡œê·¸ë˜ë° ê°€ëŠ¥í•œ êµ¬ì„± ì–¸ì–´ì…ë‹ˆë‹¤.

Dhallì€ JSON + í•¨ìˆ˜ + ìœ í˜• + ê°€ì ¸ì˜¤ê¸°ë¼ê³  ìƒê°í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

ì°¸ê³ ë¡œ Dhallì€ í”„ë¡œê·¸ë˜ë° ê°€ëŠ¥í•˜ì§€ë§Œ íŠœë§ ì™„ì „í•˜ì§€ëŠ” ì•ŠìŠµë‹ˆë‹¤. Dhallì˜ ë§ì€ ê¸°ëŠ¥ì€ ë” ê°•ë ¥í•œ ì•ˆì „ ë³´ì¥ê³¼ ë” ê°•ë ¥í•œ ë„êµ¬ë¥¼ ì œê³µí•˜ê¸° ìœ„í•´ ì´ ì œí•œì„ í™œìš©í•©ë‹ˆë‹¤.

```haskell
-- í•œ ì¤„ ì£¼ì„

{- ì—¬ëŸ¬ ì¤„ ì£¼ì„

   ìœ ë‹ˆì½”ë“œë„ ê´œì°®ìŠµë‹ˆë‹¤ ğŸ™‚

   ì´ íŒŒì¼ì€ ê° ë‹¨ê³„ì˜ ê²°ê³¼ë¥¼ ìˆ˜ì§‘í•˜ëŠ” í° ë ˆì½”ë“œë¡œ í‰ê°€ë˜ëŠ” ìœ íš¨í•œ Dhall í‘œí˜„ì‹ì…ë‹ˆë‹¤.

   íŒŒì¼ì„ í•´ì„í•˜ì—¬ ê²°ê³¼ë¥¼ ë³¼ ìˆ˜ ìˆìŠµë‹ˆë‹¤:

       $ dhall --file learndhall.dhall

   {- ì£¼ì„ì€ ì¤‘ì²©ë  ìˆ˜ ìˆìŠµë‹ˆë‹¤ -}
-}

let greeting = "Hello, world!"

let fruits = "ğŸ‹ğŸ“ğŸğŸ‰ğŸŒ"

let interpolation = "ë§›ìˆëŠ” ê³¼ì¼ì„ ì¦ê¸°ì„¸ìš”: ${fruits}"

let multilineText {- ì¸ë¼ì¸ ì£¼ì„ë„ ì‘ë™í•©ë‹ˆë‹¤ -} =
        ''
        ì„ í–‰ ê³µë°±ì€ ì—¬ëŸ¬ ì¤„ í…ìŠ¤íŠ¸ ë¦¬í„°ëŸ´ì—ì„œ ì œê±°ë©ë‹ˆë‹¤.

        ì¦‰, ê²°ê³¼ë¥¼ ë³€ê²½í•˜ì§€ ì•Šê³  í…ìŠ¤íŠ¸ ë¦¬í„°ëŸ´ì„ ììœ ë¡­ê²Œ ë“¤ì—¬ì“°ê±°ë‚˜ ë‚´ì–´ì“¸ ìˆ˜ ìˆìŠµë‹ˆë‹¤.

            ë¦¬í„°ëŸ´ ë‚´ì˜ ìƒëŒ€ ë“¤ì—¬ì“°ê¸°ëŠ” ì—¬ì „íˆ ìœ ì§€ë©ë‹ˆë‹¤.

        ê·¸ ì™¸ì—ëŠ” í…ìŠ¤íŠ¸ ë¦¬í„°ëŸ´ì´ "ë¦¬í„°ëŸ´" YAML ì—¬ëŸ¬ ì¤„ ë¬¸ìì—´ê³¼ ìœ ì‚¬í•˜ê²Œ ê·¸ëŒ€ë¡œ ìœ ì§€ë©ë‹ˆë‹¤.
        ''

let bool = True

-- ë°”ì¸ë”©ì— ëŒ€í•œ ìœ í˜• ì£¼ì„ì€ ì„ íƒ ì‚¬í•­ì´ì§€ë§Œ ìœ ìš©í•˜ë¯€ë¡œ ì‚¬ìš©í•˜ê² ìŠµë‹ˆë‹¤.
let annotation : Bool = True

let renderedBool : Text = if bool then "True" else "False"

-- ìì—°ìˆ˜ëŠ” ìŒìˆ˜ê°€ ì•„ë‹ˆë©° ë¶€í˜¸ê°€ ì—†ìŠµë‹ˆë‹¤.
let naturalNumber : Natural = 42

-- ì •ìˆ˜ëŠ” ìŒìˆ˜ì¼ ìˆ˜ ìˆì§€ë§Œ, ì–‘ìˆ˜ì´ë”ë¼ë„ ëª…ì‹œì ì¸ ë¶€í˜¸ê°€ í•„ìš”í•©ë‹ˆë‹¤.
let positiveInteger : Integer = +1

let negativeInteger : Integer = -12

let pi : Double = 3.14159265359

{- ë°±í‹±ì„ ì‚¬ìš©í•˜ì—¬ ì‹ë³„ì(ì˜ˆ: ë”°ì˜´í‘œ ë° ê³µë°±)ì— ëŒ€í•´ ë” ë„“ì€ ë¬¸ì ë²”ìœ„ë¥¼ ì‚¬ìš©í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.
-}
let `Avogadro's Number` : Double = 6.0221409e+23

let origin : { x : Double, y : Double } = { x = 0.0, y = 0.0 }

let somePrimes : List Natural = [ 2, 3, 5, 7, 11 ]

{- ìŠ¤í‚¤ë§ˆëŠ” ìœ í˜•ê³¼ ë™ì¼í•©ë‹ˆë‹¤.

   ìœ í˜•ì€ ê´€ë¡€ìƒ ëŒ€ë¬¸ìë¡œ ì‹œì‘í•˜ì§€ë§Œ, ì´ ê´€ë¡€ëŠ” ê°•ì œë˜ì§€ ì•ŠìŠµë‹ˆë‹¤.
-}
let Profile : Type
        = { person :
              { name : Text
              , age  : Natural
              }
          , address :
              { country : Text
              , state   : Text
              , city    : Text
              }
          }

let john : Profile =
        { person =
            { name = "John Doe"
            , age  = 67
            }
        , address =
            { country = "United States"
            , state   = "Pennsylvania"
            , city    = "Philadelphia"
            }
        }

let philadelphia : Text = john.address.city

{- Enum ëŒ€ì•ˆì€ ê´€ë¡€ìƒ ëŒ€ë¬¸ìë¡œ ì‹œì‘í•©ë‹ˆë‹¤. ì´ ê´€ë¡€ëŠ” ê°•ì œë˜ì§€ ì•ŠìŠµë‹ˆë‹¤.
-}
let DNA : Type = < Adenine | Cytosine | Guanine | Thymine >

let dnaSequence : List DNA = [ DNA.Thymine, DNA.Guanine, DNA.Guanine ]

let compactDNASequence : List DNA =
        let a = DNA.Adenine
        let c = DNA.Cytosine
        let g = DNA.Guanine
        let t = DNA.Thymine
        in  [ c, t, t, a, t, c, g, g, c ]

-- ê° ëŒ€ì•ˆë‹¹ í•˜ë‚˜ì˜ í•„ë“œë¥¼ ê°€ì§„ ë ˆì½”ë“œë¥¼ ì œê³µí•˜ì—¬ ì—´ê±°í˜•ì„ ë³€í™˜í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.
let theLetterG : Text =
            merge
            { Adenine  = "A"
            , Cytosine = "C"
            , Guanine  = "G"
            , Thymine  = "T"
            }
            DNA.Guanine

let presentOptionalValue : Optional Natural = Some 1

let absentOptionalValue : Optional Natural = None Natural

let points : List { x : Double, y : Double } =
        [ { x = 1.1, y = -4.2 }
        , { x = 4.4, y = -3.0 }
        , { x = 8.2, y = -5.5 }
        ]

{- `Natural -> List Natural`ì€ ì…ë ¥ ìœ í˜•ì´ `Natural`ì´ê³ 
   ì¶œë ¥ ìœ í˜•ì´ `List Natural`ì¸ í•¨ìˆ˜ì˜ ìœ í˜•ì…ë‹ˆë‹¤.

   Dhallì˜ ëª¨ë“  í•¨ìˆ˜ëŠ” ìµëª… í•¨ìˆ˜(ì¼ëª… "ëŒë‹¤")ì´ë©°,
   ì„ íƒì ìœ¼ë¡œ ì´ë¦„ì„ ì§€ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

   ì˜ˆë¥¼ ë“¤ì–´, ë‹¤ìŒ í•¨ìˆ˜ëŠ” ì´ Python ì½”ë“œì™€ ë™ì¼í•©ë‹ˆë‹¤:

       lambda n : [ n, n + 1 ]

   ... ê·¸ë¦¬ê³  ì´ JavaScript ì½”ë“œ:

       function (n) { return [ n, n + 1 ]; }
-}
let exampleFunction : Natural -> List Natural =
        \(n : Natural) -> [ n, n + 1 ]

-- Dhallì€ ìœ ë‹ˆì½”ë“œ êµ¬ë¬¸ë„ ì§€ì›í•˜ì§€ë§Œ, ì´ íŠœí† ë¦¬ì–¼ì—ì„œëŠ” ASCIIë¥¼ ì‚¬ìš©í•©ë‹ˆë‹¤.
let unicodeFunction : Natural â†’ List Natural =
        Î»(n : Natural) â†’ [ n, n + 1 ]

-- í•¨ìˆ˜ ì¸ìˆ˜ë¥¼ ê´„í˜¸ë¡œ ë¬¶ì„ í•„ìš”ê°€ ì—†ìŠµë‹ˆë‹¤.
let exampleFunctionApplication : List Natural =
        exampleFunction 2

let functionOfMultipleArguments : Natural -> Natural -> List Natural =
        \(x : Natural) -> \(y : Natural) -> [ x, y ]

let functionAppliedToMultipleArguments : List Natural =
        functionOfMultipleArguments 2 3

{- `exampleFunction`ê³¼ ë™ì¼í•˜ì§€ë§Œ í•¨ìˆ˜ì˜ ì…ë ¥ ìœ í˜•ì— ì´ë¦„ì„ ì§€ì •í–ˆìŠµë‹ˆë‹¤:
   "n"
-}
let namedArgumentType : forall (n : Natural) -> List Natural =
        \(n : Natural) -> [ n, n + 1 ]

{- í•¨ìˆ˜ì˜ ì…ë ¥ ìœ í˜•ì— ì´ë¦„ì„ ì§€ì •í•˜ë©´ ë™ì¼í•œ ìœ í˜• ë‚´ì—ì„œ ë‚˜ì¤‘ì— í•´ë‹¹ ì´ë¦„ì„ ì‚¬ìš©í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

   ì´ë ‡ê²Œ í•˜ë©´ ë‘˜ ì´ìƒì˜ ì…ë ¥ ìœ í˜•ì— ëŒ€í•´ ì‘ë™í•˜ëŠ” í•¨ìˆ˜ë¥¼ ì‘ì„±í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.
   (ì¼ëª… "ë‹¤í˜•ì„±" í•¨ìˆ˜)
-}
let duplicate : forall (a : Type) -> a -> List a =
        \(a : Type) -> \(x : a) -> [ x, x ]

let duplicatedNumber : List Natural =
        duplicate Natural 2

let duplicatedBool : List Bool =
        duplicate Bool False

{- ì–¸ì–´ì—ëŠ” ë‹¤ìŒê³¼ ê°™ì€ ì¼ë¶€ ë‚´ì¥ ë‹¤í˜•ì„± í•¨ìˆ˜ë„ ìˆìŠµë‹ˆë‹¤:

       List/head : forall (a : Type) -> List a -> Optional a
-}
let firstPrime : Optional Natural = List/head Natural somePrimes

let functionOfARecord : { x : Natural, y : Natural } -> List Natural =
        \(args : { x : Natural, y : Natural }) -> [ args.x, args.y ]

let functionAppliedToARecord : List Natural =
        functionOfARecord { x = 2, y = 5 }

{- ëª¨ë“  ìœ í˜• ë³€í™˜ì€ ëª…ì‹œì ì…ë‹ˆë‹¤.

   `Natural/show`ëŠ” ë‹¤ìŒ ìœ í˜•ì˜ ë‚´ì¥ í•¨ìˆ˜ì…ë‹ˆë‹¤:

       Natural/show : Natural -> Text

   ... `Natural` ìˆ«ìë¥¼ `Text` í‘œí˜„ìœ¼ë¡œ ë³€í™˜í•©ë‹ˆë‹¤.
-}
let typeConversion : Natural -> Text =
        \(age : Natural) -> "ì €ëŠ” ${Natural/show age}ì‚´ì…ë‹ˆë‹¤!"

-- "í…œí”Œë¦¿"ì€ ì¶œë ¥ ìœ í˜•ì´ `Text`ì¸ í•¨ìˆ˜ì™€ ë™ì¼í•©ë‹ˆë‹¤.
let mitLicense : { year : Natural, copyrightHolder : Text } -> Text =
        \(args : { year : Natural, copyrightHolder : Text }) ->
''
Copyright ${Natural/show args.year} ${args.copyrightHolder}

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
of the Software, and to permit persons to whom the Software is furnished to do
so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
''

-- í…œí”Œë¦¿ ì¸ìŠ¤í„´ìŠ¤í™”ëŠ” í•¨ìˆ˜ ì ìš©ê³¼ ë™ì¼í•©ë‹ˆë‹¤.
let templatedLicense : Text =
        mitLicense { year = 2019, copyrightHolder = "Jane Smith" }

{- URLë¡œ í‘œí˜„ì‹ì„ ê°€ì ¸ì˜¬ ìˆ˜ ìˆìŠµë‹ˆë‹¤.

   ë˜í•œ Bashì™€ ë§ˆì°¬ê°€ì§€ë¡œ ë¡œì»¬ íŒŒì¼ ì‹œìŠ¤í…œì—ì„œ ì½”ë“œë¥¼ ê°€ì ¸ì˜¬ ìˆ˜ ìˆìŠµë‹ˆë‹¤(í‘œì‹œë˜ì§€ ì•ŠìŒ).

   ë³´ì•ˆì— ë¯¼ê°í•œ ì‚¬ìš©ìëŠ” ì˜ë¯¸ë¡ ì  ë¬´ê²°ì„± ê²€ì‚¬ë¥¼ ì¶”ê°€í•˜ì—¬ ì›ê²©ìœ¼ë¡œ ê°€ì ¸ì˜¨ í‘œí˜„ì‹ì„ ê³ ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ì´ë ‡ê²Œ ê³ ì •ëœ í‘œí˜„ì‹ì„ ì¡°ì‘í•˜ë ¤ëŠ” ì‹œë„ëŠ” ì¸í„°í”„ë¦¬í„°ì—ì„œ ê±°ë¶€ë©ë‹ˆë‹¤. ê·¸ëŸ¬ë‚˜ ê°€ì ¸ì˜¨ ì½˜í…ì¸ ì˜ ë™ì‘ ë³´ì¡´ ë¦¬íŒ©í† ë§ì€ í•´ì‹œë¥¼ ë³€ê²½í•˜ì§€ ì•ŠìŠµë‹ˆë‹¤.

   ì´ë ‡ê²Œ ê³ ì •ëœ ê°€ì ¸ì˜¨ í‘œí˜„ì‹ì€ ì½˜í…ì¸  ì£¼ì†Œ ì§€ì • ê°€ëŠ¥ ì €ì¥ì†Œ(ì¼ë°˜ì ìœ¼ë¡œ `~/.cache/dhall` ì•„ë˜)ì— ë¡œì»¬ë¡œ ìºì‹œë©ë‹ˆë‹¤.
-}
let Natural/sum : List Natural -> Natural =
      https://prelude.dhall-lang.org/Natural/sum
      sha256:33f7f4c3aff62e5ecf4848f964363133452d420dcde045784518fb59fa970037

let twentyEight : Natural = Natural/sum somePrimes

-- "íŒ¨í‚¤ì§€"ëŠ” ê°€ì ¸ì˜¬ ìˆ˜ ìˆëŠ” (ì¤‘ì²©ë  ìˆ˜ ìˆëŠ”) ë ˆì½”ë“œì™€ ë™ì¼í•©ë‹ˆë‹¤.
let Prelude = https://prelude.dhall-lang.org/package.dhall

let false : Bool = Prelude.Bool.not True

-- `as Text`ë¥¼ ê°€ì ¸ì˜¤ê¸°ì— ì¶”ê°€í•˜ì—¬ íŒŒì¼ì˜ ì›ì‹œ ë‚´ìš©ì„ ê°€ì ¸ì˜¬ ìˆ˜ ìˆìŠµë‹ˆë‹¤.
let sourceCode : Text = https://prelude.dhall-lang.org/Bool/not as Text

-- í™˜ê²½ ë³€ìˆ˜ë„ ê°€ì ¸ì˜¬ ìˆ˜ ìˆìŠµë‹ˆë‹¤:
let presentWorkingDirectory = env:PWD as Text

-- ê°€ì ¸ì˜¤ê¸°ê°€ ì‹¤íŒ¨í•  ê²½ìš° ëŒ€ì²´ í‘œí˜„ì‹ì„ ì œê³µí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.
let home : Optional Text = Some env:HOME ? None Text

-- ëŒ€ì²´ í‘œí˜„ì‹ì€ ìì²´ì ìœ¼ë¡œ ëŒ€ì²´ ê°€ì ¸ì˜¤ê¸°ë¥¼ í¬í•¨í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.
let possiblyCustomPrelude =
        env:DHALL_PRELUDE
      ? https://prelude.dhall-lang.org/package.dhall

{- `generate` í•¨ìˆ˜ë¥¼ ì‚¬ìš©í•˜ì—¬ 10ëª…ì˜ ë¹Œë“œ ì‚¬ìš©ìì— ëŒ€í•œ êµ¬ì„±ì„ ìë™ ìƒì„±í•©ë‹ˆë‹¤:

       Prelude.List.generate
           : Natural -> forall (a : Type) -> (Natural -> a) -> List a
-}
let buildUsers =
        let makeUser = \(user : Text) ->
              let home       = "/home/${user}"
              let privateKey = "${home}/.ssh/id_ed25519"
              let publicKey  = "${privateKey}.pub"
              in  { home = home
                  , privateKey = privateKey
                  , publicKey = publicKey
                  }

        let buildUser =
                \(index : Natural) -> makeUser "build${Natural/show index}"

        let Config =
              { home : Text
              , privateKey : Text
              , publicKey : Text
              }

        in  Prelude.List.generate 10 Config buildUser

-- ëª¨ë“  ê²°ê³¼ë¥¼ ìµœì¢… ë ˆì½”ë“œì— í‘œì‹œí•©ë‹ˆë‹¤.
in  { greeting = greeting
    , fruits = fruits
    , interpolation = interpolation
    , multilineText = multilineText
    , bool = bool
    , annotation = annotation
    , renderedBool = renderedBool
    , naturalNumber = naturalNumber
    , positiveInteger = positiveInteger
    , negativeInteger = negativeInteger
    , pi = pi
    , `Avogadro's Number` = `Avogadro's Number`
    , origin = origin
    , somePrimes = somePrimes
    , john = john
    , philadelphia = philadelphia
    , dnaSequence = dnaSequence
    , compactDNASequence = compactDNASequence
    , theLetterG = theLetterG
    , presentOptionalValue = presentOptionalValue
    , absentOptionalValue = absentOptionalValue
    , points = points
    , exampleFunction = exampleFunction
    , unicodeFunction = unicodeFunction
    , exampleFunctionApplication = exampleFunctionApplication
    , functionOfMultipleArguments = functionOfMultipleArguments
    , functionAppliedToMultipleArguments = functionAppliedToMultipleArguments
    , namedArgumentType = namedArgumentType
    , duplicate = duplicate
    , duplicatedNumber = duplicatedNumber
    , duplicatedBool = duplicatedBool
    , firstPrime = firstPrime
    , functionOfARecord = functionOfARecord
    , functionAppliedToARecord = functionAppliedToARecord
    , typeConversion = typeConversion
    , mitLicense = mitLicense
    , templatedLicense = templatedLicense
    , twentyEight = twentyEight
    , false = false
    , sourceCode = sourceCode
    , presentWorkingDirectory = presentWorkingDirectory
    , home = home
    , buildUsers = buildUsers
    }
```

ë” ìì„¸íˆ ì•Œì•„ë³´ë ¤ë©´ ê³µì‹ ì›¹ì‚¬ì´íŠ¸ë¥¼ ë°©ë¬¸í•˜ì‹­ì‹œì˜¤. ë¸Œë¼ìš°ì €ì—ì„œ ì–¸ì–´ë¥¼ ì§ì ‘ ì‚¬ìš©í•´ ë³¼ ìˆ˜ë„ ìˆìŠµë‹ˆë‹¤:

* [https://dhall-lang.org](http://dhall-lang.org/)