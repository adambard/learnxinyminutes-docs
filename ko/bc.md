---
name: bc
contributors:
    - ["Btup"]
filename: learnbc.bc
translators:
    - ["Taeyoon Kim", "https://github.com/partrita"]
---

```bc
/*이것은 여러 줄
주석입니다.*/
# 이것은 또한 (한 줄) 주석입니다! (GNU bc에서).

    /*1. 변수 및 제어 구조*/
num = 45 /*모든 변수는 더블만 저장하며,
    문자열 상수를 직접 저장할 수 없습니다.*/
num = 45; /*모든 문장 뒤에 세미콜론을 추가할 수 있습니다.
    이것은 선택 사항입니다.*/
/*블록은 {} 연산자로 표시됩니다(C와 유사):*/
while(num < 50) {
    num += 1 /*num=num+1과 동일합니다.
    a = a op b는 a op= b와 동일합니다.*/
}
/*그리고 ++(증가) 및 --(감소) 연산자가 있습니다.*/
/*세 가지 특수 변수가 있습니다:
scale: 더블 숫자의 스케일을 정의합니다.
ibase: 입력의 밑을 정의합니다.
obase: 출력의 밑을 정의합니다.*/
/*If 절:*/
hour = read() /*숫자 입력*/

if(hour < 12) { /*연산자는 C와 정확히 같습니다.*/
    print "Good morning\n" /*"print"는 쉼표로 구분된 문자열 또는 변수를 출력합니다.*/
} else if(hour == 12) {
    print "Hello\n"
    /*이스케이프 시퀀스는 문자열에서 \로 시작합니다.
    이스케이프 시퀀스를 더 명확하게 하기 위해, 여기에
    bc에서 작동하는 간단한 목록이 있습니다:
    \b: 백스페이스
    \c: 캐리지 리턴
    \n: 줄 바꿈
    \t: 탭
    \\: 백슬래시*/
} else {
    print "Good afternoon\n"
}

/*C와 마찬가지로 0만 거짓입니다.*/
num = 0
if(!num) {print "false\n"}

/*C와 달리 bc에는 ?: 연산자가 없습니다. 예를 들어,
 이 코드 블록은 오류를 발생시킵니다:
a = (num) ? 1 : 0
그러나 하나를 시뮬레이션할 수 있습니다:*/
a = (num) && (1) || (0) /*&&는 and, ||는 or입니다*/

/*For 루프*/
num = 0
for(i = 1; i <= 100; i++) {/*C for 루프와 유사합니다.*/
    num += i
}

    /*2.함수 및 배열*/
define fac(n) { /*define을 사용하여 함수를 정의합니다.*/
    if(n == 1 || n == 0) {
        return 1 /*값을 반환합니다*/
    }
    return n * fac(n - 1) /*재귀가 가능합니다*/
}

/*클로저 및 익명 함수는 불가능합니다.*/

num = fac(4) /*24*/

/*다음은 지역 변수의 예입니다:*/
define x(n) {
    auto x
    x = 1
    return n + x
}
x(3) /*4*/
print x /*함수 밖에서는 x에 접근할 수 없습니다.*/
/*배열은 C 배열과 동일합니다.*/
for(i = 0; i <= 3; i++) {
    a[i] = 1
}
/*이렇게 접근합니다:*/
print a[0], " ", a[1], " ", a[2], " ", a[3], "\n"
quit /*프로그램이 종료되도록 이 줄을 추가합니다.
이 줄은 선택 사항입니다.*/
```

이 간단한 계산기를 즐기십시오! (또는 이 프로그래밍 언어를 정확히 말하면.)

이 전체 프로그램은 GNU bc로 작성되었습니다. 실행하려면 ```bc learnbc.bc```를 사용하십시오.

```